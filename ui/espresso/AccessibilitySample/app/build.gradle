apply plugin: "com.android.application"

android {
    compileSdk 33
    defaultConfig {
        applicationId "com.example.android.testing.espresso.AccessibilitySample"
        minSdkVersion 19
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        multiDexEnabled true
    }
    productFlavors {
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        managedDevices {
            devices {
                // run with ../gradlew  nexusOneApi30DebugAndroidTest
                nexusOneApi30(com.android.build.api.dsl.ManagedVirtualDevice) {
                    // A lower resolution device is used here for better emulator performance
                    device = "Nexus One"
                    apiLevel = 30
                    // TODO: switch to aosp-atd image once it supports browser intents
                    systemImageSource = "aosp"
                }
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    namespace "com.example.android.testing.espresso.AccessibilitySample"
    testNamespace "com.example.android.testing.espresso.AccessibilitySample.test"
    lint {
        abortOnError false
    }
}



tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    // App dependencies
    implementation "androidx.annotation:annotation:" + rootProject.androidxAnnotationVersion;
    implementation "com.google.guava:guava:" + rootProject.guavaVersion
    implementation "androidx.multidex:multidex:2.0.1"

    // Testing-only dependencies
    androidTestImplementation "androidx.test:core:" + rootProject.coreVersion
    androidTestImplementation "androidx.test.ext:junit:" + rootProject.extJUnitVersion
    androidTestImplementation "androidx.test:runner:" + rootProject.runnerVersion
    androidTestImplementation "androidx.test.espresso:espresso-core:" + rootProject.espressoVersion
    androidTestImplementation("androidx.test.espresso:espresso-accessibility:" + rootProject.espressoVersion) {
        // workaround for https://github.com/android/android-test/issues/861
        exclude group: "org.checkerframework", module: "checker"
    }
    androidTestImplementation "com.google.truth:truth:1.1.3"
}
