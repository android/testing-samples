load("@gmaven_rules//:defs.bzl", "gmaven_artifact")
load("//:common_defs.bzl", "androidxLibVersion", "minSdkVersion", "targetSdkVersion")

licenses(["notice"])  # Apache 2.0

android_library(
    name = "MultiWindowSampleLib",
    srcs = glob(["app/src/main/**/*.java"]),
    custom_package = "com.example.android.testing.espresso.MultiWindowSample",
    manifest = "app/src/main/AndroidManifest.xml",
    resource_files = glob(["app/src/main/res/**/*"]),
    deps = [
        gmaven_artifact("androidx.annotation:annotation:jar:" + androidxLibVersion),
        "@com_google_guava_guava//jar",
    ],
)

android_binary(
    name = "MultiWindowSample",
    custom_package = "com.example.android.testing.espresso.MultiWindowSample",
    manifest = "app/src/main/AppManifest.xml",
    manifest_values = {
        "minSdkVersion": minSdkVersion,
        "targetSdkVersion": targetSdkVersion,
    },
    deps = [":MultiWindowSampleLib"],
)

android_library(
    name = "MultiWindowSampleTestLib",
    srcs = glob(["app/src/androidTest/**/*.java"]),
    custom_package = "com.example.android.testing.espresso.MultiWindowSample.test",
    deps = [
        ":MultiWindowSampleLib",
        "//:test_deps",
    ],
)

android_binary(
    name = "MultiWindowSampleTest",
    custom_package = "com.example.android.testing.espresso.MultiWindowSample.test",
    instruments = ":MultiWindowSample",
    manifest = "app/src/androidTest/AndroidManifest.xml",
    manifest_values = {
        "minSdkVersion": minSdkVersion,
        "targetSdkVersion": targetSdkVersion,
    },
    deps = [":MultiWindowSampleTestLib"],
)

API_LEVELS = [
    "19_x86",
    "21_x86",
    "22_x86",
    "23_x86",
]

[android_instrumentation_test(
    name = "MultiWindowSampleInstrumentationTest_%s" % API_LEVEL,
    target_device = "@android_test_support//tools/android/emulated_devices/generic_phone:android_%s_qemu2" % API_LEVEL,
    test_app = ":MultiWindowSampleTest",
) for API_LEVEL in API_LEVELS]
