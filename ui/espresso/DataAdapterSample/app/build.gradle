apply plugin: "com.android.application"

android {
    compileSdk 33
    defaultConfig {
        applicationId "com.example.android.testing.espresso.DataAdapterSample"
        minSdkVersion 19
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }
    productFlavors {
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        managedDevices {
            devices {
                // run with ../gradlew  nexusOneApi30DebugAndroidTest
                nexusOneApi30(com.android.build.api.dsl.ManagedVirtualDevice) {
                    // A lower resolution device is used here for better emulator performance
                    device = "Nexus One"
                    apiLevel = 30
                    // Also use the AOSP ATD image for better emulator performance
                    systemImageSource = "aosp-atd"
                }
            }
        }
    }
    // share test source between local test and androidTest
    sourceSets {
        test {
            java.srcDir "src/sharedTest/java"
        }
        androidTest {
            java.srcDir "src/sharedTest/java"
        }
    }
    namespace "com.example.android.testing.espresso.DataAdapterSample"
    testNamespace "com.example.android.testing.espresso.DataAdapterSample.test"
}

dependencies {
    // App dependencies
    implementation "androidx.annotation:annotation:" + rootProject.androidxAnnotationVersion;
    implementation "com.google.guava:guava:" + rootProject.guavaVersion

    // Testing-only dependencies
    androidTestImplementation "androidx.test:core:" + rootProject.coreVersion;
    androidTestImplementation "androidx.test.ext:junit:" + rootProject.extJUnitVersion;
    androidTestImplementation "androidx.test:runner:" + rootProject.runnerVersion;
    androidTestImplementation "androidx.test.espresso:espresso-core:" + rootProject.espressoVersion;

    testImplementation "androidx.test:core:" + rootProject.coreVersion;
    testImplementation "androidx.test.ext:junit:" + rootProject.extJUnitVersion;
    testImplementation "androidx.test.espresso:espresso-core:" + rootProject.espressoVersion;
    testImplementation "org.robolectric:robolectric:" + rootProject.robolectricVersion
}
