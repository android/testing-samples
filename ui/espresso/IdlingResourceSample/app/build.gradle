/*
 * Copyright 2016, The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: "com.android.application"

android {
    compileSdk 33

    defaultConfig {
        applicationId "com.example.android.testing.espresso.IdlingResourceSample"
        minSdkVersion 19
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        managedDevices {
            devices {
                // run with ../gradlew  nexusOneApi30DebugAndroidTest
                nexusOneApi30(com.android.build.api.dsl.ManagedVirtualDevice) {
                    // A lower resolution device is used here for better emulator performance
                    device = "Nexus One"
                    apiLevel = 30
                    // Also use the AOSP ATD image for better emulator performance
                    systemImageSource = "aosp-atd"
                }
            }
        }
    }
    namespace "com.example.android.testing.espresso.IdlingResourceSample"
    testNamespace "com.example.android.testing.espresso.IdlingResourceSample.test"
    lint {
        abortOnError false
    }
}

dependencies {
    // App dependencies
    implementation "androidx.annotation:annotation:" + rootProject.androidxAnnotationVersion
    implementation "com.google.guava:guava:" + rootProject.guavaVersion

    // Testing-only dependencies
    androidTestImplementation "androidx.test:core:" + rootProject.coreVersion
    androidTestImplementation "androidx.test.ext:junit:" + rootProject.extJUnitVersion
    androidTestImplementation "androidx.test:runner:" + rootProject.runnerVersion
    androidTestImplementation "androidx.test.espresso:espresso-core:" + rootProject.espressoVersion
    // Note that espresso-idling-resource is used in the code under test.
    implementation "androidx.test.espresso:espresso-idling-resource:" + rootProject.espressoVersion
}
