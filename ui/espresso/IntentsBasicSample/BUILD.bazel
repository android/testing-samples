load("@rules_jvm_external//:defs.bzl", "artifact")
load("//:common_defs.bzl", "minSdkVersion", "targetSdkVersion")

licenses(["notice"])  # Apache 2.0

android_library(
    name = "IntentsBasicSampleLib",
    srcs = glob(["app/src/main/**/*.java"]),
    custom_package = "com.example.android.testing.espresso.IntentsBasicSample",
    manifest = "app/src/main/AndroidManifest.xml",
    resource_files = glob(["app/src/main/res/**/*"]),
    deps = [
        "@maven//:androidx_annotation_annotation",
        "@maven//:androidx_core_core",
        "@maven//:com_google_guava_guava",
    ],
)

android_binary(
    name = "IntentsBasicSample",
    custom_package = "com.example.android.testing.espresso.IntentsBasicSample",
    manifest = "app/src/main/AppManifest.xml",
    manifest_values = {
        "minSdkVersion": minSdkVersion,
        "targetSdkVersion": targetSdkVersion,
    },
    deps = [":IntentsBasicSampleLib"],
)

android_library(
    name = "IntentsBasicSampleTestLib",
    srcs = glob(["app/src/sharedTest/**/*.java"]),
    custom_package = "com.example.android.testing.espresso.IntentsBasicSample.test",
    deps = [
        ":IntentsBasicSampleLib",
        "//:test_deps",
        "@maven//:androidx_test_espresso_espresso_intents",
        "@maven//:androidx_test_ext_truth",
        "@maven//:com_google_truth_truth",
        "@maven//:org_robolectric_robolectric",
        "@maven//:org_robolectric_annotations",
        "@maven//:junit_junit",
    ],
)

android_binary(
    name = "IntentsBasicSampleTest",
    custom_package = "com.example.android.testing.espresso.IntentsBasicSample.test",
    instruments = ":IntentsBasicSample",
    manifest = "app/src/androidTest/AndroidManifest.xml",
    manifest_values = {
        "minSdkVersion": minSdkVersion,
        "targetSdkVersion": targetSdkVersion,
    },
    deps = [":IntentsBasicSampleTestLib"],
)

API_LEVELS = [
    "19_x86",
    "21_x86",
    "22_x86",
    "23_x86",
]

[android_instrumentation_test(
    name = "IntentsBasicSampleInstrumentationTest_%s" % API_LEVEL,
    target_device = "@android_test_support//tools/android/emulated_devices/generic_phone:android_%s_qemu2" % API_LEVEL,
    test_app = ":IntentsBasicSampleTest",
) for API_LEVEL in API_LEVELS]
