apply plugin: "com.android.application"

apply plugin: "kotlin-android"

android {
    compileSdk 33
    defaultConfig {
        applicationId "com.example.android.testing.espresso.FragmentScenarioSample"
        minSdkVersion 19
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }
    // share the unified tests
    sourceSets {
        test {
            java.srcDir "src/sharedTest/java"
        }
        androidTest {
            java.srcDir "src/sharedTest/java"
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        managedDevices {
            devices {
                // run with ../gradlew  nexusOneApi30DebugAndroidTest
                nexusOneApi30(com.android.build.api.dsl.ManagedVirtualDevice) {
                    // A lower resolution device is used here for better emulator performance
                    device = "Nexus One"
                    apiLevel = 30
                    // Also use the AOSP ATD image for better emulator performance
                    systemImageSource = "aosp-atd"
                }
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    namespace "com.example.android.testing.espresso.fragmentscenario"
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    implementation "androidx.appcompat:appcompat:" + rootProject.androidxCompatVersion
    implementation "androidx.core:core-ktx:" + rootProject.androidxCoreVersion
    implementation "androidx.fragment:fragment-ktx:" + rootProject.androidxFragmentVersion

    // TODO: ideally this would only be present in test scope
    debugImplementation "androidx.fragment:fragment-testing:" + rootProject.androidxFragmentVersion
    debugImplementation "androidx.test:core:" + rootProject.coreVersion

    testImplementation "junit:junit:4.12"
    testImplementation "org.robolectric:robolectric:" + rootProject.robolectricVersion
    testImplementation "androidx.test:core:" + rootProject.coreVersion
    testImplementation "androidx.test.ext:junit:" + rootProject.extJUnitVersion
    testImplementation "androidx.test.espresso:espresso-core:" + rootProject.espressoVersion
    testImplementation "com.google.truth:truth:" + rootProject.truthVersion
    testAnnotationProcessor "com.google.auto.service:auto-service:1.0-rc4"

    androidTestImplementation "androidx.test:core:" + rootProject.coreVersion
    androidTestImplementation "androidx.test.ext:junit:" + rootProject.extJUnitVersion
    androidTestImplementation "androidx.test:runner:" + rootProject.runnerVersion
    androidTestImplementation "androidx.test.espresso:espresso-core:" + rootProject.espressoVersion
    androidTestImplementation "androidx.fragment:fragment-testing:" + rootProject.androidxFragmentVersion
    androidTestImplementation "com.google.truth:truth:" + rootProject.truthVersion
    androidTestImplementation "org.robolectric:annotations:" + rootProject.robolectricVersion
}
