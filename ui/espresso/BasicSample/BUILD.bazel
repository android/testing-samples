licenses(["notice"])  # Apache 2.0

android_library(
    name = "BasicSampleLib",
    srcs = glob(["app/src/main/**/*.java"]),
    custom_package = "com.example.android.testing.espresso.BasicSample",
    manifest = "app/src/main/AndroidManifest.xml",
    resource_files = glob(["app/src/main/res/**/*"]),
    deps = ["@com_google_guava_guava//jar"],
)

android_library(
    name = "BasicSampleTestLib",
    srcs = glob(["app/src/androidTest/**/*.java"]),
    custom_package = "com.example.android.testing.espresso.BasicSample",
    deps = [
        ":BasicSampleLib",
        "//:test_deps",
    ],
)

android_binary(
    name = "BasicSample",
    custom_package = "com.example.android.testing.espresso.BasicSample",
    manifest = "app/src/main/AppManifest.xml",
    deps = [":BasicSampleLib"],
)

android_binary(
    name = "BasicSampleTest",
    custom_package = "com.example.android.testing.espresso.BasicSample",
    instruments = ":BasicSample",
    manifest = "app/src/androidTest/AndroidManifest.xml",
    deps = [":BasicSampleTestLib"],
)

API_LEVELS = [
    "19_x86",
    "21_x86",
    "22_x86",
    "23_x86",
    "24_x86",
]

[android_instrumentation_test(
    name = "BasicSampleInstrumentationTest_%s" % API_LEVEL,
    target_device = "@android_test_support//tools/android/emulated_devices/generic_phone:android_%s_qemu2" % API_LEVEL,
    test_app = ":BasicSampleTest",
) for API_LEVEL in API_LEVELS]
