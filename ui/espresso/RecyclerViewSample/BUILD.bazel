load("@rules_jvm_external//:defs.bzl", "artifact")
load("//:common_defs.bzl", "minSdkVersion", "targetSdkVersion")

licenses(["notice"])  # Apache 2.0

android_library(
    name = "RecyclerViewSampleLib",
    srcs = glob(["app/src/main/**/*.java"]),
    custom_package = "com.example.android.testing.espresso.RecyclerViewSample",
    manifest = "app/src/main/AndroidManifest.xml",
    resource_files = glob(["app/src/main/res/**/*"]),
    deps = [
        artifact("androidx.recyclerview:recyclerview"),
        artifact("androidx.annotation:annotation"),
        artifact("com.google.guava:guava"),
    ],
)

android_binary(
    name = "RecyclerViewSample",
    custom_package = "com.example.android.testing.espresso.RecyclerViewSample",
    manifest = "app/src/main/AppManifest.xml",
    deps = [":RecyclerViewSampleLib"],
)

android_library(
    name = "RecyclerViewSampleTestLib",
    srcs = glob(["app/src/androidTest/**/*.java"]),
    custom_package = "com.example.android.testing.espresso.RecyclerViewSample.test",
    deps = [
        artifact("androidx.test.espresso:espresso-contrib"),
        ":RecyclerViewSampleLib",
        "//:test_deps",
    ],
)

android_binary(
    name = "RecyclerViewSampleTest",
    custom_package = "com.example.android.testing.espresso.RecyclerViewSample.test",
    instruments = ":RecyclerViewSample",
    manifest = "app/src/androidTest/AndroidManifest.xml",
    manifest_values = {
        "minSdkVersion": minSdkVersion,
        "targetSdkVersion": targetSdkVersion,
    },
    deps = [":RecyclerViewSampleTestLib"],
)

API_LEVELS = [
    "19_x86",
    "21_x86",
    "22_x86",
    "23_x86",
]

[android_instrumentation_test(
    name = "RecyclerViewSampleInstrumentationTest_%s" % API_LEVEL,
    target_device = "@android_test_support//tools/android/emulated_devices/generic_phone:android_%s_qemu2" % API_LEVEL,
    test_app = ":RecyclerViewSampleTest",
) for API_LEVEL in API_LEVELS]
